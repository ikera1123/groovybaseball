buildscript {
    repositories {
       jcenter()
    }
                
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    } 
}
                        
apply plugin:'groovy'
apply plugin:'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin:'eclipse'
apply plugin:'eclipse-wtp'

repositories {
    jcenter()
}

gretty {
    integrationTestTask = 'intTest'
}

task intTest(type:Test) {
}

check.dependsOn intTest

project.ext.db = file("$buildDir/baseball.h2.db")

task generateTestDb(dependsOn:classes, type:JavaExec) {
    onlyIf { !db.exists() }
    main = 'service.populate_stadium_data'
    classpath sourceSets.main.runtimeClasspath
}

task deployDb(dependsOn:generateTestDb, type:Copy) {
    from db
    into 'src/main/webapp/build'
}

war.dependsOn deployDb

task removeDb(type:Delete) {
    delete 'src/main/webapp/build'
}

clean.dependsOn removeDb

task wrapper(type:Wrapper) {
    gradleVersion = '2.0'
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.1'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime "cglib:cglib-nodep:2.2"
    runtime 'com.h2database:h2:1.2.140'
}
